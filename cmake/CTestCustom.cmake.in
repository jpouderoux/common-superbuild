function (project_ignore_regexes variable project)
  set(exceptions)
  list(APPEND exceptions
    "${project}/src"
    "${project}\\\\src")
  string(TOLOWER "${project}" lower_project)
  if ((APPLE OR WIN32) AND (NOT lower_project STREQUAL project))
    if (WIN32)
      list(APPEND exceptions
        "${lower_project}\\\\src")
    else ()
      list(APPEND exceptions
        "${lower_project}/src")
    endif ()
  endif ()

  set("${variable}"
    ${${variable}}
    ${exceptions}
    PARENT_SCOPE)
endfunction ()

macro (ignore_project_warnings project)
  project_ignore_regexes(CTEST_CUSTOM_WARNING_EXCEPTION "${project}")
endmacro ()

macro (ignore_project_errors project)
  project_ignore_regexes(CTEST_CUSTOM_ERROR_EXCEPTION "${project}")
endmacro ()

ignore_project_warnings(szip)

set(extra_ctest_custom_files "@extra_ctest_custom_files@")
foreach (extra_ctest_custom_file IN LISTS extra_ctest_custom_files)
  include("${extra_ctest_custom_file}")
endforeach ()
